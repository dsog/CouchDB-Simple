#!/usr/bin/env perl
use strict;
use warnings;
use CouchDB::Simple;
use JSON qw(from_json);
use App::Rad;

my $db = CouchDB::Simple->new();

App::Rad->run();

sub setup {
    my $c = shift;
    $c->register_commands({
        add    => 'add a document, content is read from stdin',
        delete => 'delete a document',
        get    => 'get a document',
        update => 'update a document, content is read from stdin',
    });
}

sub pre_process {
    my $c = shift;
    return if !$c->cmd || $c->cmd eq 'help';
    my ($db, $id) = @{$c->argv};
    die "db name and id of document is required\n"
        unless defined $id and defined $db;
    $c->stash->{db} = $db;
    $c->stash->{id} = $id;
}

sub add {
    my $c = shift;
    my $json = do { local $/; <STDIN> };
    my $person = from_json($json);
    return $db->create_document($c->stash->{db}, $c->stash->{id}, $person);
}

sub delete {
    my $c = shift;
    return $db->delete_document($c->stash->{db}, $c->stash->{id});
}

sub update {
    my $c = shift;
    my $json = do { local $/; <STDIN> };
    my $person = from_json($json);
    return $db->update_document($c->stash->{db}, $c->stash->{id}, $person, 0);
}

sub get {
    my $c = shift;
    return $db->get_document($c->stash->{db}, $c->stash->{id});
}

#my $res = $db->getAllDocuments;


__END__

=head1 NAME

couchdb-simple - A command line interface to CouchDB

=head1 SYNOPSIS

  Usage: couchdb-simple command [arguments]

  Available Commands:
      add   	add a document, content is read from stdin
      delete	delete a document
      get   	get a document
      help  	show syntax and available commands
      update	update a document, content is read from stdin


  # All commands require a database name document id.
  $ couchdb-simple delete mydb foo

  # Some commands expect to read text from stdin. You can type some text
  # and then hit <ctrl-d> when you are done.
  $ couchdb-simple add mydb foo
  {"foo":"bar"}
  <ctrl-d>

  # Or you could use piping or redirection.
  $ echo '{"foo":"bar"}' | couchdb-simple add mydb foo
  $ couchdb-simple add mydb foo < foo.json

=head1 DESCRIPTION

This is a command line tool to interface with CouchDB.

=head1 COMMANDS

=over

=item help

Print a brief help message.

=item add I<db> I<id>

Add a document to db with the given id.
The contents of the document will be read from stdin.

=item delete I<db> I<id>

Delete the document in db with the given id.

=item get I<db> I<id>

Retrieve the document from db with the given id.

=item update I<db> I<id>

Update the document in db with the given id.
The contents of the document will be read from stdin.

=back

